pragma ComponentBehavior: Bound

import QtQuick
import Quickshell
import Caelestia
import Caelestia.Config
import Caelestia.Services
import qs.components
import qs.components.controls
import qs.services
import qs.utils

Item {
    id: root

    required property ScreenState screenState

    implicitWidth: bg.implicitWidth
    implicitHeight: bg.implicitHeight

    StyledRect {
        id: bg

        anchors.centerIn: parent
        implicitWidth: row.implicitWidth + Tokens.padding.large * 2
        implicitHeight: row.implicitHeight + Tokens.padding.large * 2
        radius: Tokens.rounding.extraLarge
        color: Qt.alpha(Colours.tPalette.m3surfaceContainer, 0.85)

        Row {
            id: row

            anchors.centerIn: parent
            spacing: Tokens.spacing.large

            SessionButton {
                id: logout

                icon: Config.session.icons.logout
                command: Config.session.commands.logout

                KeyNavigation.right: shutdown

                Component.onCompleted: forceActiveFocus()

                Connections {
                    function onLauncherChanged(): void {
                        if (!root.screenState.launcher)
                            logout.forceActiveFocus();
                    }

                    target: root.screenState
                }
            }

            SessionButton {
                id: shutdown

                icon: Config.session.icons.shutdown
                command: Config.session.commands.shutdown

                KeyNavigation.left: logout
                KeyNavigation.right: hibernate
            }

            SessionButton {
                id: hibernate

                icon: Config.session.icons.hibernate
                command: Config.session.commands.hibernate

                KeyNavigation.left: shutdown
                KeyNavigation.right: reboot
            }

            SessionButton {
                id: reboot

                icon: Config.session.icons.reboot
                command: Config.session.commands.reboot

                KeyNavigation.left: hibernate
            }
        }
    }

    component SessionButton: IconButton {
        id: button

        required property list<string> command

        function exec(): void {
            if (!SessionManager.exec(command))
                Quickshell.execDetached(command);
        }

        implicitWidth: Tokens.sizes.session.button
        implicitHeight: Tokens.sizes.session.button

        inactiveColour: activeFocus ? Colours.palette.m3secondaryContainer : Colours.tPalette.m3surfaceContainer
        inactiveOnColour: activeFocus ? Colours.palette.m3onSecondaryContainer : Colours.palette.m3onSurface
        radius: pressed ? Tokens.rounding.medium : activeFocus ? Tokens.rounding.extraLarge : Tokens.rounding.largeIncreased
        font: Tokens.font.icon.builders.large.scale(1.3).build()
        onClicked: exec()

        Keys.onEnterPressed: exec()
        Keys.onReturnPressed: exec()
        Keys.onEscapePressed: root.screenState.session = false
        Keys.onPressed: event => {
            if (!Config.session.vimKeybinds)
                return;

            if (event.modifiers & Qt.ControlModifier) {
                if ((event.key === Qt.Key_J || event.key === Qt.Key_N) && KeyNavigation.right) {
                    KeyNavigation.right.focus = true;
                    event.accepted = true;
                } else if ((event.key === Qt.Key_K || event.key === Qt.Key_P) && KeyNavigation.left) {
                    KeyNavigation.left.focus = true;
                    event.accepted = true;
                }
            } else if (event.key === Qt.Key_Tab && KeyNavigation.right) {
                KeyNavigation.right.focus = true;
                event.accepted = true;
            } else if (event.key === Qt.Key_Backtab || (event.key === Qt.Key_Tab && (event.modifiers & Qt.ShiftModifier))) {
                if (KeyNavigation.left) {
                    KeyNavigation.left.focus = true;
                    event.accepted = true;
                }
            }
        }
    }
}
